dependencies {
    compile(project(":grails-datastore-gorm-hibernate-core")) {
        exclude group:'org.hibernate', module:'hibernate-core'
        exclude group:'org.hibernate', module:'hibernate-validator'
        exclude group:'org.hibernate', module:'hibernate-ehcache'
        exclude group:'org.hibernate', module:'hibernate-commons-annotations'
    }
    compile(project(":grails-datastore-gorm-support")) {
        exclude group:'org.grails', module:'grails-datastore-gorm-hibernate-core'
    }

    // Web dependencies provided
    provided "javax.servlet:javax.servlet-api:$servletApiVersion"
    provided(project(":grails-datastore-web"))

    String hibVersion = "${hibernate5Version}.Final"

    compile 'org.springframework:spring-orm:4.2.3.RELEASE'
    compile("org.hibernate:hibernate-core:$hibVersion") {
        exclude group:'commons-logging', module:'commons-logging'
        exclude group:'dom4j', module:'dom4j'
        exclude group:'com.h2database', module:'h2'
        exclude group:'commons-collections', module:'commons-collections'
        exclude group:'org.slf4j', module:'jcl-over-slf4j'
        exclude group:'org.slf4j', module:'slf4j-api'
        exclude group:'org.slf4j', module:'slf4j-log4j12'
        exclude group:'xml-apis', module:'xml-apis'
    }
    compile('org.hibernate:hibernate-validator:5.2.2.Final') {
        exclude group:'commons-logging', module:'commons-logging'
        exclude group:'commons-collections', module:'commons-collections'
        exclude group:'org.slf4j', module:'slf4j-api'
    }
    runtime('javax.el:javax.el-api:2.2.4')

    runtime('dom4j:dom4j:1.6.1') {
        exclude group: 'xml-apis', module:'xml-apis'
    }

    provided ("net.sf.ehcache:ehcache-core:2.4.8") {
        exclude group: 'commons-logging', module:'commons-logging'
    }

    provided ("org.hibernate:hibernate-ehcache:$hibVersion") {
        exclude group:'commons-collections', module:'commons-collections'
        exclude group:'commons-logging', module:'commons-logging'
        exclude group:'com.h2database', module:'h2'
        exclude group:'dom4j', module:'dom4j'
        exclude group:'net.sf.ehcache', module:'ehcache'
        exclude group:'net.sf.ehcache', module:'ehcache-core'
        exclude group:'org.hibernate', module:'hibernate-core'
        exclude group:'org.slf4j', module:'jcl-over-slf4j'
        exclude group:'org.slf4j', module:'slf4j-api'
        exclude group:'org.slf4j', module:'slf4j-log4j12'
        exclude group:'xml-apis', module:'xml-apis'
    }

    testCompile "org.codehaus.groovy:groovy-sql:$groovyVersion"
    testCompile project(":grails-datastore-gorm-tck")
    testCompile "com.h2database:h2:1.3.164"

    testCompile "net.sf.ehcache:ehcache-core:2.4.6"
    testCompile "org.hibernate:hibernate-ehcache:$hibVersion"

    provided "javax.servlet:javax.servlet-api:$servletApiVersion"

    def tomcatVersion = '7.0.50'
    testCompile "org.apache.tomcat:tomcat-jdbc:$tomcatVersion"
    testRuntime "org.apache.tomcat.embed:tomcat-embed-logging-log4j:$tomcatVersion"

    // The groovydoc task needs the Hibernate 4.x jars in the classpath
    documentation("org.hibernate:hibernate-core:${hibernateVersion}.Final")

}

eclipse {
    classpath {
        file {
            whenMerged { classpath ->
                // move grails-datastore-gorm-hibernate-core reference to end
                def hibernateCoreReference = classpath.entries.find { entry -> entry.kind == 'src' && entry.path == "/grails-datastore-gorm-hibernate-core" }
                if (hibernateCoreReference) {
                    classpath.entries.remove hibernateCoreReference
                    classpath.entries << hibernateCoreReference
                }
            }
        }
    }
}

test {
    if (!isTravisBuild) {
        forkEvery = 30
    }
}


